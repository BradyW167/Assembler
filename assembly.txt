// KEYBOARD > 0 ? Black : White
(LOOP)
    // Get keyboard value
    @KBD
    D=M
	
    // Jump to on if it's more than 0
    @ON
    D;JGT
	
    // Jump to off otherwise
    @OFF
    0;JMP
	
// Turn the screen on and loop
(ON)
    // Set the draw value to -1 for black
    @R0
    M=-1
	
    // Draw screen
    @DRAW
    0;JMP
	
// Turn the screen off and loop
(OFF)
    // Set the draw value to 0 for white
    @R0
    M=0
	
    // Draw screen
    @DRAW
    0;JMP
	
// Set the screen to R0 and loop
(DRAW)
    // R1 is set to 8192 and decremented to get the memory register
    // for every pixel
    @8191
    D=A
    @R1
    M=D
	
    // Loop through each pixel on screen and set it to R0
    (NEXT)
        // Calculate the position
        @R1
        D=M
        @pos
        M=D
        @SCREEN
        D=A
        @pos
        M=D+M

        // Draw value stored in R0 at pos
        @R0
        D=M
        @pos
        A=M
        M=D

        // Decrement the counter
        @R1
        D=M-1
        M=D

        // Next pixel if the counter >= 0
        @NEXT
        D;JGE
		
    // Loop back around.
    @LOOP
    0;JMP
